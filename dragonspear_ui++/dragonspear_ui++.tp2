BACKUP ~dragonspear_ui++/backup~
AUTHOR ~Pecca (https://forums.beamdog.com/discussion/50357/mod-dragonspear-ui-v2-31/p1)~
VERSION ~22.01.3~

LANGUAGE
	~English~ ~dragonspear_ui++/tra~ ~dragonspear_ui++/tra/english.tra~
LANGUAGE
	~Polski (by Etamin)~ ~dragonspear_ui++/tra/english.tra~ ~dragonspear_ui++/tra/polish.tra~
LANGUAGE
	~Italian (by Aedan)~ ~dragonspear_ui++/tra/english.tra~ ~dragonspear_ui++/tra/italian.tra~
LANGUAGE
	~Russian (by d0lphin)~ ~dragonspear_ui++/tra/russian.tra~ ~dragonspear_ui++/tra/russian.tra~
LANGUAGE
	~Brazilian Portuguese (by Felipefpl)~ ~dragonspear_ui++/tra/brazilian portuguese.tra~ ~dragonspear_ui++/tra/brazilian portuguese.tra~


/////////////////////////////////////////////////////////////////////////////////
///  M_BG.LUA BACKUP
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1004 NO_LOG_RECORD

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

REQUIRE_PREDICATE FILE_EXISTS ~override/M_BG.lua~ @1005

COPY_EXISTING ~M_BG.lua~ ~dragonspear_ui++/backup-M_BG~


/////////////////////////////////////////////////////////////////////////////////
///  SOD GUI OVERHAUL CORE COMPONENT
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1000

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

ACTION_MATCH 0 WITH
	0 WHEN GAME_IS ~eet~ BEGIN OUTER_SPRINT game "eet" END
	0 WHEN GAME_IS ~bgee~ BEGIN
		ACTION_IF GAME_INCLUDES ~sod~ BEGIN
			OUTER_SPRINT game "sod"
		END ELSE BEGIN
			OUTER_SPRINT game "bg1"
		END
	END
	0 WHEN GAME_IS ~bg2ee~ BEGIN OUTER_SPRINT game "bg2" END
	DEFAULT FAIL "unhandled match case"
END

OUTER_SPRINT build_folder "%MOD_FOLDER%/build"
MKDIR "%build_folder%"

ACTION_IF "%WEIDU_OS%" STRING_EQUAL "win32" BEGIN
	AT_NOW exit_code "%MOD_FOLDER%/bin/build.bat %WEIDU_ARCH% %game% 2> %build_folder%/error.log"
END ELSE BEGIN
	AT_NOW exit_code "%MOD_FOLDER%/bin/build.sh %game% 2> %build_folder%/error.log"
END

ACTION_MATCH exit_code WITH
	0 BEGIN /* all good */ END
	42 BEGIN
		WARN "This mod requires lua5.2+ installed."

		ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL "osx" BEGIN
			WARN "You can install it using homebrew or macports:%LNL%"
			   ^ "https://brew.sh%LNL%"
			   ^ "https://www.macports.org%LNL%"
		END ELSE BEGIN
			WARN "You can install it using your OS package manager."
		END

		FAIL "Missing or unsupported Lua version"
	END
	DEFAULT
		FAIL "An error happened file generating UI.menu file"
END


COPY "%build_folder%/ui.menu" ~override~
COPY ~dragonspear_ui++/content-common/all~ ~override~

ACTION_IF GAME_IS ~bg2ee eet~ OR NOT GAME_INCLUDES ~sod~ BEGIN
	COPY ~dragonspear_ui++/content-common/eet-bg2~ ~override~
END

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
	COPY ~dragonspear_ui++/content-common/eet-sod~ ~override~
	COPY ~dragonspear_ui++/content-sod~ ~override~
END

ACTION_IF GAME_IS ~bg2ee~ THEN BEGIN
	COPY ~dragonspear_ui++/content-bg2~ ~override~
END

ACTION_IF GAME_IS ~eet~ THEN BEGIN
	COPY ~dragonspear_ui++/content-common/eet-sod~ ~override~
	COPY ~dragonspear_ui++/content-eet~ ~override~

	COPY ~dragonspear_ui++/content-sod/rgdback1.wbm~ ~override/rgdback1.wbm~
	COPY ~dragonspear_ui++/content-sod/rgdback1.wbm~ ~override/rgdback7.wbm~
	COPY ~dragonspear_ui++/content-sod/rgdback2.wbm~ ~override/rgdback2.wbm~
	COPY ~dragonspear_ui++/content-sod/rgdback3.wbm~ ~override/rgdback5.wbm~
	COPY ~dragonspear_ui++/content-bg2/rgdback1.wbm~ ~override/rgdback3.wbm~
	COPY ~dragonspear_ui++/content-bg2/rgdback2.wbm~ ~override/rgdback4.wbm~
	COPY ~dragonspear_ui++/content-bg2/rgdback3.wbm~ ~override/rgdback6.wbm~
END

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~M_BG.lua~) BEGIN
<<<<<<<< .../M_BG.lua
BGImages = {
{"NO PORTRAIT","D","NOPORTM"}
}
>>>>>>>>
	COPY ~.../M_BG.lua~ ~override~
END ELSE BEGIN
	COPY_EXISTING + ~M_BG.lua~ ~override~
		REPLACE_EVALUATE CASE_INSENSITIVE ~\({[%TAB% ]*"[^"]+"[%TAB% ]*,[%TAB% ]*"[MFD]"[%TAB% ]*,[%TAB% ]*"\)\([^"]+\)\("[%TAB% ]*},?[%WNL%%LNL%%MNL%%TAB% ]*\)~ BEGIN
			PATCH_IF (NOT FILE_EXISTS ~%USER_DIRECTORY%/portraits/%MATCH2%.bmp~) BEGIN
				SPRINT MATCH1 ~~
				SPRINT MATCH2 ~~
				SPRINT MATCH3 ~~
			END
		END ~%MATCH1%%MATCH2%%MATCH3%~
	BUT_ONLY
END

COPY_EXISTING ~M_BG.lua~ ~override~
	PATCH_BASH_FOR ~%USER_DIRECTORY%/portraits~ ~^.+bmp$~ BEGIN
		PATCH_IF (STRING_LENGTH ~%BASH_FOR_RES%~ < 8) AND (NOT FILE_CONTAINS_EVALUATED (~M_BG.lua~ ~"%BASH_FOR_RES%"~)) BEGIN
			PATCH_IF ((~%BASH_FOR_RES%~ STRING_CONTAINS_REGEXP ~^[Mm]#~)=0) BEGIN
				SPRINT gender ~M~
			END ELSE PATCH_IF ((~%BASH_FOR_RES%~ STRING_CONTAINS_REGEXP ~^[Ff]#~)=0) BEGIN
				SPRINT gender ~F~
			END ELSE BEGIN
				SPRINT gender ~D~
			END
			REPLACE_TEXTUALLY ~\({[%TAB% ]*"[^"]+"[%TAB% ]*,[%TAB% ]*"[MFD]"[%TAB% ]*,[%TAB% ]*"[^"]+"[%TAB% ]*}\),?[%WNL%%LNL%%MNL%%TAB% ]*}~ ~\1,
{"%BASH_FOR_RES%","%gender%","%BASH_FOR_RES%"},
}~
		END
	END


/////////////////////////////////////////////////////////////////////////////////
///  TRANSPARENT TOOLTIP BACKGROUND
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1007

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

COPY ~dragonspear_ui++/content-tooltip~ ~override~


/////////////////////////////////////////////////////////////////////////////////
///  UPDATE DIALOG.TLK STRINGS
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1001

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

STRING_SET ~23084~ @101

STRING_SET ~11947~ @106
STRING_SET ~11949~ @107
STRING_SET ~11951~ @108
STRING_SET ~11952~ @109
STRING_SET ~11953~ @110

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
STRING_SET ~24366~ @102
END
ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
STRING_SET ~5684~ @102
END


/////////////////////////////////////////////////////////////////////////////////
///  QUICKSAVE SLOTS NUMBER
/////////////////////////////////////////////////////////////////////////////////

BEGIN ~1~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 1


BEGIN ~2~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 2


BEGIN ~3~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 3


BEGIN ~4~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 4


BEGIN ~5~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 5


BEGIN ~6~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 6


/////////////////////////////////////////////////////////////////////////////////
///  PORTRAIT PICKER UPDATE
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1002

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

REQUIRE_PREDICATE FILE_EXISTS ~override/RGCGAPP.png~ @1006

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~M_BG.lua~) BEGIN
<<<<<<<< .../M_BG.lua
BGImages = {
{"NO PORTRAIT","D","NOPORTM"}
}
>>>>>>>>
	COPY + ~.../M_BG.lua~ ~override~
END ELSE BEGIN
	COPY_EXISTING + ~M_BG.lua~ ~override~
		REPLACE_EVALUATE CASE_INSENSITIVE ~\({[%TAB% ]*"[^"]+"[%TAB% ]*,[%TAB% ]*"[MFD]"[%TAB% ]*,[%TAB% ]*"\)\([^"]+\)\("[%TAB% ]*},?[%WNL%%LNL%%MNL%%TAB% ]*\)~ BEGIN
			PATCH_IF (NOT FILE_EXISTS ~%USER_DIRECTORY%/portraits/%MATCH2%.bmp~) BEGIN
				SPRINT MATCH1 ~~
				SPRINT MATCH2 ~~
				SPRINT MATCH3 ~~
			END
		END ~%MATCH1%%MATCH2%%MATCH3%~
	BUT_ONLY
END

COPY_EXISTING + ~M_BG.lua~ ~override~
	PATCH_BASH_FOR ~%USER_DIRECTORY%/portraits~ ~^.+bmp$~ BEGIN
		PATCH_IF (STRING_LENGTH ~%BASH_FOR_RES%~ < 8) AND (NOT FILE_CONTAINS_EVALUATED (~M_BG.lua~ ~"%BASH_FOR_RES%"~)) BEGIN
			PATCH_IF ((~%BASH_FOR_RES%~ STRING_CONTAINS_REGEXP ~^[Mm]#~)=0) BEGIN
				SPRINT gender ~M~
			END ELSE PATCH_IF ((~%BASH_FOR_RES%~ STRING_CONTAINS_REGEXP ~^[Ff]#~)=0) BEGIN
				SPRINT gender ~F~
			END ELSE BEGIN
				SPRINT gender ~D~
			END
			REPLACE_TEXTUALLY ~\({[%TAB% ]*"[^"]+"[%TAB% ]*,[%TAB% ]*"[MFD]"[%TAB% ]*,[%TAB% ]*"[^"]+"[%TAB% ]*}\),?[%WNL%%LNL%%MNL%%TAB% ]*}~ ~\1,
{"%BASH_FOR_RES%","%gender%","%BASH_FOR_RES%"},
}~
		END
	END

/////////////////////////////////////////////////////////////////////////////////
///  BLURRY PORTRAITS FIX
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1009

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

ACTION_DEFINE_ARRAY blurry_portraits BEGIN schael coran hephern END
ACTION_PHP_EACH blurry_portraits AS _ => v BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%v%l.bmp~) BEGIN
		COPY_EXISTING ~%v%l.bmp~ ~override/%v%s.bmp~
		COPY_EXISTING ~%v%l.bmp~ ~override/%v%m.bmp~
	END
END
